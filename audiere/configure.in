dnl Process this file with autoconf to produce a configure script.

dnl Boilerplate code.

AC_INIT(src/audiere.h)
AC_CONFIG_AUX_DIR(config)

AC_CANONICAL_SYSTEM

MAJOR_VERSION=1
MINOR_VERSION=9
PATCH_VERSION=1
VERSION=$MAJOR_VERSION.$MINOR_VERSION.$PATCH_VERSION
PACKAGE="audiere"

AC_SUBST(MAJOR_VERSION)
AC_SUBST(MINOR_VERSION)
AC_SUBST(PATCH_VERSION)
AC_SUBST(VERSION)
AC_SUBST(PACKAGE)

AM_INIT_AUTOMAKE($PACKAGE, $VERSION)

dnl Checks for options.

AC_ARG_ENABLE(debug,
    AC_HELP_STRING(--enable-debug, build debug),
    CXXFLAGS="$CXXFLAGS -g"
    AC_DEFINE(DEBUG))

dnl Checks for programs.

AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_PROG_CXXCPP
AC_PROG_MAKE_SET
AM_PROG_LIBTOOL

dnl Checks for libraries.

dnl If building on Cygwin, link with winmm when using OpenAL
case $host in
  *cygwin*)
    OPENAL_LIBS=-lwinmm
    ;;
esac

dnl If building with gcc, build with all warnings except "class has virtual
dnl functions but non-virtual destructor".
if [[ "x$GXX" = "xyes" ]]; then
   CXXFLAGS="$CXXFLAGS -Wall -Wno-non-virtual-dtor"
fi
if [[ "x$GCC" = "xyes" ]]; then
   CFLAGS="$CFLAGS -Wall -Wno-non-virtual-dtor"
fi

dnl Look for pthreads
AC_CHECK_LIB(pthread, pthread_create,
    LIBS="-lpthread $LIBS"
    EXTRA_LIBS="-lpthread $EXTRA_LIBS"
  ,
    AC_MSG_RESULT(no)
    AC_MSG_ERROR(could not find pthread library))

dnl Look for OpenAL
AC_CHECK_LIB(openal, alBufferData,
    HAVE_OPENAL=true
    AC_DEFINE(HAVE_OPENAL)
    LIBS="-lopenal $OPENAL_LIBS $LIBS"
    EXTRA_LIBS="-lopenal $OPENAL_LIBS $EXTRA_LIBS",, $OPENAL_LIBS)
AM_CONDITIONAL(HAVE_OPENAL, test "x$HAVE_OPENAL" = "xtrue")

dnl Look for FLAC
AC_CHECK_LIB(FLAC, FLAC__seekable_stream_decoder_process_single,
    LIBS="-lFLAC++ -lFLAC -lm $LIBS"
    EXTRA_LIBS="-lFLAC++ -lFLAC -lm $EXTRA_LIBS"
  ,
    AC_MSG_ERROR([libFLAC not found.  Install FLAC from http://flac.sourceforge.net/]),
  ,
    -lm)

dnl Look for the POSIX realtime clock
AC_SEARCH_LIBS(clock_gettime, rt,
    AC_DEFINE(HAVE_CLOCK_GETTIME))

dnl Look for SGI audio
AC_CHECK_LIB(audio, alNewConfig,
    HAVE_AL=true
    AC_DEFINE(HAVE_AL)
    LIBS="-laudio $LIBS"
    EXTRA_LIBS="-laudio $EXTRA_LIBS")
AM_CONDITIONAL(HAVE_AL, test "x$HAVE_AL" = "xtrue")

dnl Look for wxWindows
AC_PATH_PROG(WX_CONFIG, wx-config, no, $PATH:/usr/local/bin)
if [[ "$WX_CONFIG" = "no" ]] ; then
    AC_MSG_WARN([wxWindows not found, not building wxPlayer.
        See http://www.wxwindows.org/])
else
    HAVE_WXWINDOWS=true
    AC_DEFINE(HAVE_WXWINDOWS)
    WX_LIBS=`$WX_CONFIG --libs`
    WX_CPPFLAGS=`$WX_CONFIG --cxxflags`
    AC_SUBST(WX_LIBS)
    AC_SUBST(WX_CPPFLAGS)
fi

AM_CONDITIONAL(HAVE_WXWINDOWS, test "x$HAVE_WXWINDOWS" = "xtrue")

dnl Checks for header files.

AC_HEADER_STDC
AC_CHECK_HEADER(unistd.h)
AC_CHECK_HEADER(sys/soundcard.h,
    HAVE_OSS=true
    AC_DEFINE(HAVE_OSS))
AC_CHECK_HEADER(vorbis/vorbisfile.h,
    LIBS="-lvorbisfile -lvorbis -logg $LIBS"
    EXTRA_LIBS="-lvorbisfile -lvorbis -logg $EXTRA_LIBS"
  ,
    AC_MSG_RESULT(no)
    AC_MSG_ERROR([Could not find vorbisfile header.  You do not appear to have libvorbis installed.
Get libvorbis from http://www.vorbis.com/]))

AM_CONDITIONAL(HAVE_OSS, test "x$HAVE_OSS" = "xtrue")

dnl Checks for typedefs, structures, and compiler characteristics.

AC_C_BIGENDIAN

dnl Output makefiles.

AC_SUBST(WORDS_BIGENDIAN)
AC_SUBST(EXTRA_LIBS)
AC_OUTPUT(
        audiere-config
	Makefile
	doc/Makefile
        examples/Makefile
        examples/simple/Makefile
        examples/wxPlayer/Makefile
        src/Makefile
        src/mikmod/Makefile
        src/mikmod/mikmod/Makefile
        src/mikmod/mikmod/loaders/Makefile
        src/mikmod/mikmod/virtch/Makefile
        src/mikmod/mikmod/virtch/resfilter/Makefile
        src/mikmod/mmio/Makefile
        src/mpegsound/Makefile
        test/Makefile
        test/buffer/Makefile
        test/device/Makefile
        test/interactive/Makefile
        test/performance/Makefile,
    [chmod a+x audiere-config])
